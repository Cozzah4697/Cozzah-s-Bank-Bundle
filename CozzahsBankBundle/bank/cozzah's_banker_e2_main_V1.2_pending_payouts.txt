@name Cozzah's Banker E2 Main

@inputs [HUD]:wirelink
@outputs [TimeLeft]:number

@persist [MemberData Racks Guards PendingPayouts]:table
@persist [Snapshot CurrentRackID HUDX HUDY]:number
@persist [CurrentCustomer]:entity

#include "CozzahsBankBundle/includes/functions"
#include "CozzahsBankBundle/includes/membership_configuration"

if(first() || dupefinished()) {    
    # ========== HUD POSITION SETTINGS ==========
    # Change these numbers to move the HUD around your screen
    # HUDX: Left/Right position (0 = far left, 100 = far right)
    # HUDY: Up/Down position (0 = top, 100 = bottom)
    HUDX = 7.8  # Default: 7.8 (left side of screen)
    HUDY = 73   # Default: 73 (lower portion of screen)
    if(HUDY > 100 || HUDY < 0) { error("INVALID HUD POSITION") }
    if(HUDX > 100 || HUDX < 0) { error("INVALID HUD POSITION") } # just wanted the warning gone :)
    # ============================================
    
    Snapshot = 0
    CurrentRackID = 0  # Initialize the last aimed rack ID
    
    MemberData = table()
    Racks = table()
    Guards = table()
    PendingPayouts = table()
    
    CurrentCustomer = _NO_ENTITY
}

function number checkRack(Entity:entity) {
    if (Entity:isValid()) {
        let Class = Entity:keyvalues()["classname", string]
        return Class:startsWith("printer_rack") || Class:startsWith("bitminer_tower")
    }
    return 0
}

# Replace the entire checkProfit() function with this:
function checkProfit(Customer:entity) {
    let Amount = owner():money() - Snapshot
    
    if(Amount > 1000) {        
        let Name = Customer:name():upper()
        let AimEntity = owner():aimEntity()
        let RackID = AimEntity:id()
        let SteamID = Customer:steamID()

        let Tax = Guards:exists(SteamID) ? 0 : getTax(Customer, MemberData)
        let TaxAmount = Amount * Tax
        let Profit = Amount - TaxAmount
        
        if(Amount == getTierCost(TIER_SILVER) || Amount == getTierCost(TIER_GOLD) || Amount == getTierCost(TIER_DIAMOND)) {
            owner():msg("{!yellow [" + BankName + "]} {! Auto Payment aborted due to being detected as a membership payment}. if this is a mistake use chat command {yellow '*WIRE " + Customer:name() + " " + Profit + "'}")
            break
        }
        
        # Update the tax total for this rack
        if(Racks[RackID, table]) {
            let CurrentTotal = Racks[RackID, table]["tax_total", number]
            Racks[RackID, table]["tax_total", number] = CurrentTotal + TaxAmount
        }
        
        owner():msg("{!yellow [" + BankName + "]} You collected: {green $" + formatNumber(TaxAmount) + "} in taxes.")
        
        # Check if there's already a pending payout for this player
        if(!PendingPayouts:exists(SteamID)) {
            # Create new payout entry
            PendingPayouts[SteamID, table] = table(
                "total_tax" = TaxAmount,
                "total_profit" = Profit,
                "rack_count" = 1,
                "tax_rate" = Tax,
                "name" = Name
            )
            
            # Start timer to send message after 2 seconds
            timer("payout_" + SteamID, 5, function() {
                if(PendingPayouts:exists(SteamID)) {
                    let PayoutData = PendingPayouts[SteamID, table]
                    let FinalTaxAmount = PayoutData["total_tax", number]
                    let FinalProfit = PayoutData["total_profit", number]
                    let RackCount = PayoutData["rack_count", number]
                    let FinalTax = PayoutData["tax_rate", number]
                    let FinalName = PayoutData["name", string]
                    
                    let FinalCustomer = SteamID:findPlayer()
                    if(FinalCustomer:isValid()) {
                        let PlayerTier = getMemberDataTier(FinalCustomer, MemberData)
                        let TierName = getTierName(PlayerTier):upper()
                        let Colour = getTierColor(PlayerTier)
                        
                        let RackText = RackCount > 1 ? " from " + RackCount + " racks" : ""
                        
                        FinalCustomer:msg("{!yellow [" + BankName + "]} Thanks for banking with us, {yellow " + FinalName + "}. You received: {green $" + formatNumber(FinalProfit) + "}" + RackText)
                        
                        if(Guards:exists(SteamID)) {
                            FinalCustomer:msg("{!yellow [" + BankName + "]} You paid {green " + formatNumber(FinalTaxAmount) + " (" + FinalTax*100 + "%)} for being a {blue security guard}")
                        }
                        elseif(TierName == "COPPER") {
                            FinalCustomer:msg("{!yellow [" + BankName + "]} You paid {! " + formatNumber(FinalTaxAmount) + " (" + FinalTax*100 + "%)} due to being a {" + Colour + " " + TierName + "} Member. Find a membership screen to reduce {green TAX} and get {green INSURANCE}")
                        }
                        else {
                            FinalCustomer:msg("{!yellow [" + BankName + "]} You paid {green " + formatNumber(FinalTaxAmount) + " (" + FinalTax*100 + "%)} thanks to being a {" + Colour + " " + TierName + "} Member")
                        }
                        
                        moneyGive(FinalCustomer, FinalProfit)
                    }
                    
                    # Clear the pending payout
                    PendingPayouts:remove(SteamID)
                }
            })
        }
        else {
            # Add to existing payout
            let PayoutData = PendingPayouts[SteamID, table]
            PayoutData["total_tax", number] = PayoutData["total_tax", number] + TaxAmount
            PayoutData["total_profit", number] = PayoutData["total_profit", number] + Profit
            PayoutData["rack_count", number] = PayoutData["rack_count", number] + 1
        }

        Snapshot = 0
    }
}

function checkCollection() {
    let AimEntity = owner():aimEntity()
    if(checkRack(AimEntity)) {
        let Customer = _NO_ENTITY
        let RackID = AimEntity:id()
        
        # Try to get customer from table first
        if(Racks[RackID, table]) {
            let RackData = Racks[RackID, table]
            let SteamID = RackData["owner", string]
            Customer = SteamID:findPlayer()
            
            # If player not found by SteamID, fall back to entity owner
            if(!Customer:isValid()) {
                Customer = AimEntity:owner()
            }
        }
        else {
            # If rack not in table, use entity owner and add to table
            Customer = AimEntity:owner()
            if(Customer:isValid()) {
                Racks[RackID, table] = table("owner" = Customer:steamID(), "tax_total" = 0)
                print("Added new rack: " + RackID + " owned by: " + Customer:name())
            }
        }
        
        # Collection display handling
        let SaveSnapshot = function() {
            if(Customer:isValid()) {
                let Class = AimEntity:keyvalues()["classname", string]
                let EntityType = ""
                if(Class:startsWith("printer_rack")) { EntityType = "RACK" }
                elseif(Class:startsWith("bitminer_tower")) { EntityType = "TOWER" }
                         
                let W = egpScrW(owner())
                let H = egpScrH(owner())
                
                HUD:egpBox(1, vec2(W*(HUDX/100),H*((HUDY+5.5)/100)), vec2(W*(11.7/100),H*(15/100)))
                HUD:egpColor(1, vec4(50,50,50,150))
                HUD:egpBox(2, vec2(W*(HUDX/100),H*(HUDY/100)), vec2(W*(11.7/100),H*(4/100)))
                HUD:egpColor(2, vec4(40,40,40,150))
                
                HUD:egpText(3, limitNameLength(Customer:name():upper(),10) + "'S " + EntityType, vec2(W*((HUDX-4.8)/100),H*((HUDY-1)/100)))
                HUD:egpSize(3,30)
                HUD:egpText(4, "Customer: " + Customer:name(), vec2(W*((HUDX-4.8)/100),H*((HUDY+3)/100)))
                HUD:egpSize(4,25)
                    
                HUD:egpText(5, "Tier:", vec2(W*((HUDX-4.8)/100),H*((HUDY+6)/100)))
                HUD:egpSize(5,25)
                
                if(Guards:exists(Customer:steamID())) {
                    HUD:egpText(6, "Guard", vec2(W*((HUDX-2.8)/100),H*((HUDY+6)/100)))
                    HUD:egpSize(6,25)
                    HUD:egpColor(6, vec4(20,20,200,255))
                }
                else {
                    let PlayerTier = getMemberDataTier(Customer, MemberData)
                    let TierName = getTierName(PlayerTier):upper()
                    HUD:egpText(6, TierName, vec2(W*((HUDX-2.8)/100),H*((HUDY+6)/100)))
                    HUD:egpSize(6,25)
                    HUD:egpColor(6, vec4(getTierColorR(PlayerTier), getTierColorG(PlayerTier), getTierColorB(PlayerTier), 255))
                }
                # Display total tax collected for this rack
                let TaxTotal = 0
                if(Racks[RackID, table]) {
                    TaxTotal = Racks[RackID, table]["tax_total", number]
                }
                HUD:egpText(7, "Tax Collected: $" + formatNumber(TaxTotal), vec2(W*((HUDX-4.8)/100),H*((HUDY+9)/100)))
                HUD:egpSize(7,25)
                HUD:egpColor(7, vec4(0,255,0,255))
            }
            else {
                HUD:egpClear()
            }
            Snapshot = owner():money()
        }
        
        # Check if the customer changed OR rack ID changed
        if(Customer != CurrentCustomer || RackID != CurrentRackID) {
            # Force update HUD when customer or rack ID changes
            CurrentCustomer = Customer
            CurrentRackID = RackID
            Snapshot = 0
        }
        
        if(!Snapshot) {
            SaveSnapshot()
        }
        else {
            checkProfit(Customer)
        }
    }
    else {
        CurrentCustomer = _NO_ENTITY
        CurrentRackID = 0
        Snapshot = 0
        HUD:egpClear()
    }
}

# Function to set a manual owner for the last aimed rack
function void setRackOwner(Player:entity) {
    if(!Player:isValid()) {
        owner():msg("{!yellow [" + BankName + "]} {red Invalid player specified.}")
        return
    }
    
    if(CurrentRackID == 0) {
        owner():msg("{!yellow [" + BankName + "]} {red You need to look at a rack first.}")
        return
    }
    
    # Get the entity for the last aimed rack ID
    let RackEntity = entity(CurrentRackID)
    if(!RackEntity:isValid() || !checkRack(RackEntity)) {
        owner():msg("{!yellow [" + BankName + "]} {red The previously aimed rack is no longer valid.}")
        return
    }
    
    # Update or create the rack entry in the Racks table
    if(Racks[CurrentRackID, table]) {
        # Update existing rack entry
        let OldOwnerSteamID = Racks[CurrentRackID, table]["owner", string]
        let OldOwner = OldOwnerSteamID:findPlayer()
        let OldOwnerName = OldOwner:isValid() ? OldOwner:name() : "Unknown"
        
        Racks[CurrentRackID, table]["owner", string] = Player:steamID()
        owner():msg("{!yellow [" + BankName + "]} {green Owner changed from " + OldOwnerName + " to " + Player:name() + " for rack #" + CurrentRackID + "}")
    } else {
        # Create new rack entry
        Racks[CurrentRackID, table] = table("owner" = Player:steamID(), "tax_total" = 0)
        owner():msg("{!yellow [" + BankName + "]} {green Set " + Player:name() + " as the owner of rack #" + CurrentRackID + "}")
    }
    
    # Inform the player that they are now the owner
    Player:msg("{!yellow [" + BankName + "]} {green " + owner():name() + " has assigned you as the owner of a rack}")
}

# sends all customers a bank closing notifications
function void close(Minutes:number, Amount:number) {
    let Time = 60 * Minutes
    let NotifNum = Amount
    let Interval = Time / NotifNum
    print("Time = " + Time)
    print("NotifNum = " + NotifNum)
    print("Interval = " + Interval)
    
    function void sendMessage(TimeLeft:string) {
        foreach(_:number, RackData:table = Racks) {
            let SteamID = RackData["owner", string]
            let Customer = SteamID:findPlayer()
            if(Customer:isValid()) {
                Customer:msg("{! [" + BankName + " ALERT]}  " + BankName:lower() + " bank will be closing in {yellow " + TimeLeft + "}. Please pick up your printers or they will be {red destroyed}.")
            }
        }
    }
    
    # Send the first message immediately when function is called
    let TimeFormat = formatTimeInSeconds(Time)
    if(TimeFormat != "") {
        sendMessage(TimeFormat)
        print("{! [" + BankName + " ALERT]} Closing notification sent. {yellow " + TimeFormat + "} till closing.")
        Time = Time - Interval
    }
    else { print("{! [ERROR] } {red 'TimeFormat' = null }") }
    
    timer("closing", Interval, NotifNum, function() {
        if(Time > 0) {
            let TimeFormat = formatTimeInSeconds(Time)
            if(TimeFormat != "") {
                sendMessage(TimeFormat)
                print("{! [" + BankName + " ALERT]} Closing notification sent. {yellow " + TimeFormat + "} till closing.") 
                Time = Time - Interval
            }
            else { print("{! [ERROR] } {red 'TimeFormat' = null }") }
        }
        else {
            stoptimer("closing")
            foreach(_:number, RackData:table = Racks) {
                let SteamID = RackData["owner", string]
                let Customer = SteamID:findPlayer()
                if(Customer:isValid()) {
                    Customer:msg("{! [" + BankName + " ALERT]} {red  " + BankName:lower() + " bank has closed. If you have not picked up your printers they will now be } {! destroyed}{red .}")
                }
            }
            print("{! [" + BankName + " ALERT]} Bank closed, you may {! destroy } remaining printers") 
        }
    })
}

# Function to display all customers and their tiers
function void displayAllCustomers() {
    let CustomerCount = 0
    
    # Header display
    owner():msg("{#FFBF00 === CUSTOMER LIST ===}")
    
    # Loop through all racks
    foreach(_:number, RackData:table = Racks) {
        if(RackData["owner", string]) {
            let SteamID = RackData["owner", string]
            let Customer = SteamID:findPlayer()
            
            if(Customer:isValid()) {
                # Get tier name for this customer
                let PlayerTier = getMemberDataTier(Customer, MemberData)
                let TierName = getTierName(PlayerTier):upper()
                let TierColor = getTierColor(PlayerTier)
                let TaxTotal = RackData["tax_total", number] || 0
                
                # Display customer info with color based on tier
                owner():msg("{" + TierColor + " " + Customer:name() + " - " + TierName + " TIER} - Tax: {green $" + formatNumber(TaxTotal) + "}")
                
                CustomerCount++
            }
        }
    }
    
    # Display count message if no customers found
    if(CustomerCount == 0) {
        owner():msg("{#FFBF00 No active customers found.}")
    }
    else {
        owner():msg("{#FFBF00 Total Customers: }{green " + CustomerCount + "}")
    }
    
    # Footer display
    owner():msg("{#FFBF00 ====================}")
}

# Function to display total tax profit from all racks
function void displayTotalProfit() {
    let TotalTax = 0
    let RackCount = 0
    
    # Loop through all racks and sum their tax_total values
    foreach(_:number, RackData:table = Racks) {
        if(RackData["tax_total", number]) {
            TotalTax += RackData["tax_total", number]
            RackCount++
        }
    }
    
    # Header display
    owner():msg("{#FFBF00 === PROFIT SUMMARY ===}")
    
    # Show total profit and count of racks
    owner():msg("{#FFBF00 Total Tax Profit: }{green $" + formatNumber(TotalTax) + "}")
    owner():msg("{#FFBF00 Number of Racks: }{green " + RackCount + "}")
    
    # Calculate average profit per rack if there are any racks
    if(RackCount > 0) {
        let AveragePerRack = TotalTax / RackCount
        owner():msg("{#FFBF00 Average per Rack: }{green $" + formatNumber(AveragePerRack) + "}")
    }
    
    # Footer display
    owner():msg("{#FFBF00 =====================}")
}

# Function to send raided notification to all unique rack owners
function void sendRaidedNotification() {
    let NotifiedPlayers = table()
    let PlayerCount = 0
    
    # Loop through all racks
    foreach(_:number, RackData:table = Racks) {
        if(RackData["owner", string]) {
            let SteamID = RackData["owner", string]
            
            # Only notify each player once
            if(!NotifiedPlayers:exists(SteamID)) {
                let Customer = SteamID:findPlayer()
                
                if(Customer:isValid()) {
                    Customer:msg("{!yellow [" + BankName + " ALERT]} {red The bank has been raided!} You will receive your {green insurance money} shortly.")
                    NotifiedPlayers[SteamID, number] = 1
                    PlayerCount++
                }
            }
        }
    }
    
    # Confirm to admin
    if(PlayerCount > 0) {
        owner():msg("{!yellow [" + BankName + "]} {green Raided notification sent to " + PlayerCount + " unique customers.}")
    } else {
        owner():msg("{!yellow [" + BankName + "]} {red No customers found to notify.}")
    }
}

function void sendReq() {
    broadcastRemoteEvent(table())
} 

function void displayAdminHelp() {
    
    # Send header
    owner():msg("{#FFBF00 === ADMIN COMMANDS ===}")
    
    # Admin commands
    owner():msg("{#FFBF00 *help} - Shows this admin help message")
    owner():msg("{#FFBF00 *close 'Minutes' 'NotifAmount'} - Sends closing notification")
    owner():msg("{#FFBF00 *customers} - Shows list of all customers")
    owner():msg("{#FFBF00 *profit} - Shows total profit summary")
    owner():msg("{#FFBF00 *wire 'player' 'amount'} - Sends money to player")
    owner():msg("{#FFBF00 *owner 'player'} - Sets player as owner of last aimed rack")  # Added new command
    owner():msg("{#FFBF00 *raided} - Notifies all customers that bank was raided")
    
    # Send footer
    owner():msg("{#FFBF00 =======================}")
}

function void cmdAdmin(Command:string, Args:array) {
    switch(Command) {
        case "HELP",
            displayAdminHelp()
        break
        case "WIRE",
            if(Args:count() >= 2) {
                let PlayerName = Args[1, string]
                let Player = PlayerName:findPlayer()
                let Amount = Args[2, string]:toNumber()
                if(Player:isValid() && Amount > 1000) {
                    moneyGive(Player,Amount)
                    owner():msg("{yellow [" + BankName + " E2] } YOU HAVE SENT {green " + formatNumber(Amount) + "} TO {*" + Player:name():upper() + "}")
                    Player:msg("{yellow [" + BankName + " E2] } {*" + limitNameLength(BankName,10) + "} HAS SENT YOU {green " + formatNumber(Amount) + "}")
                }
                else { print("{! Player or Amount INVALID}") }      
            }
            else { print("{! usage} - {yellow *wire 'player' 'amount'} amount must be bigger than 100") }
        break
        case "OWNER",  # New command to manually set rack owner
            if(Args:count() >= 1) {
                let PlayerName = Args[1, string]
                let Player = PlayerName:findPlayer()
                if(Player:isValid()) {
                    setRackOwner(Player)
                } else {
                    owner():msg("{!yellow [" + BankName + "]} {red Player not found.}")
                }
            } else {
                owner():msg("{!yellow [" + BankName + "]} {red Usage: *owner 'player'}")
            }
        break
        case "PROFIT",
            displayTotalProfit()
        break
        case "CLOSE",
            if(Args:count() >= 1) {
                let Minutes = Args[1, string]:toNumber()
                let NotifNum = Args[2, string]:toNumber()
                close(Minutes, NotifNum)
            }
        break
        case "CUSTOMERS",
            displayAllCustomers()
        break
        case "RAIDED",
           sendRaidedNotification()
       break
    }
}

if(first() || dupefinished()) {
    sendReq()
    timer(60,0,function() {
        sendReq()
    })
    timer(0.1, 0, function() {
        checkCollection()
    })
    timer("Timer",600,0,function() {
        print("{! [" + BankName + "] } {!green time to colect from printer}")
        owner():soundPlay(0,1,"zmlab2/cash.wav")
    })
    
    timer("TimeLeft",1,0,function() { TimeLeft = timerTimeLeft("Timer") })
}

event remote(Sender:entity, Player:entity, Payload:table) {    
    if (Player == owner()) {
        if(Sender == findE2ByName("Cozzah's bank security v1.2",200)) {
            Guards = Payload
            print("{blue " + Sender:getName() + "} table has been loaded with {yellow " + Payload:count() + "} entries.")
        }
        elseif(Sender == findE2ByName("Cozzah's Banker E2 Membership",200)) {
            MemberData = Payload
            print("{green " + Sender:getName() + "} table has been loaded with {yellow " + Payload:count() + "} entries.")
        }
    }
}

event chat(Player:entity, Message:string, _:number) {
    if(Player == owner() && Message:sub(1, 1) == "*") {
        local Args = Message:explode(" ")
        local Command = Args[1, string]:upper()
        Args:remove(1)
        cmdAdmin(Command:sub(2), Args)
        hideChat(1)
    }
}
