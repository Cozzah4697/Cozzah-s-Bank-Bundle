@name CS:GO Bomb Defusal
@inputs [SiteA SiteB]:wirelink
@persist [Elements TeamT TeamCT]:table
@persist [BombState PlantProgress DefuseProgress]:number
@persist [BombPos]:vector2 [BombSite Planter Defuser]:string
@persist [SelectedPlanter]:entity

# BombState: 0=None, 1=Planting, 2=Planted, 3=Defusing, 4=Defused

if (first() || dupefinished()) {
    Elements = table()
    TeamT = table()
    TeamCT = table()
    BombState = 0
    PlantProgress = 0
    DefuseProgress = 0
    BombPos = vec2(0, 0)
    BombSite = ""
    Planter = ""
    Defuser = ""
    SelectedPlanter = _NO_ENTITY
    
    function number table:get(Name:string) {
        if (This:exists(Name)) {
            return This[Name, number]
        }
        local FreeID = This:flip():ncount() + 1
        if (FreeID >= 300) {
            error("Too many elements being drawn.")
        }
        This[Name, number] = FreeID
        return FreeID
    }

    function void wirelink:clearScreen() {
        This:egpClear()
    }

    function egpobject box(EGP:wirelink, ID:number, ARGS:table) {
        if (ARGS["outline", number]) {
            return EGP:egpRoundedBoxOutline(ID, ARGS)
        }
        return EGP:egpRoundedBox(ID, ARGS)
    }

    function egpobject line(EGP:wirelink, ID:number, ARGS:table) {
        return EGP:egpBox(ID, ARGS)
    }

    function void entity:msg(Text:string) {
        if (!This:isValid()) { return }
        This:tauSendChatMsg(Text)
    }

    function void drawBomb(EGP:wirelink, PosX:number, PosY:number) {
        local OffsetX = PosX - 111
        local OffsetY = PosY - 67
        
        box(EGP, Elements:get("Rectangle521"), table("x"=90.0+OffsetX, "y"=38.0+OffsetY, "w"=42.0, "h"=54.0, "r"=0, "g"=0, "b"=0, "radius"=1.0))
        box(EGP, Elements:get("Rectangle525"), table("x"=91.0+OffsetX, "y"=39.0+OffsetY, "w"=40.0, "h"=52.0, "r"=255, "g"=255, "b"=255, "radius"=1.0))
        box(EGP, Elements:get("Rectangle531"), table("x"=102.0+OffsetX, "y"=45.0+OffsetY, "w"=18.0, "h"=4.0, "r"=0, "g"=0, "b"=0, "radius"=1.0))
        box(EGP, Elements:get("Rectangle523"), table("x"=90.0+OffsetX, "y"=76.0+OffsetY, "w"=42.0, "h"=6.0, "r"=0, "g"=0, "b"=0, "radius"=0))
        box(EGP, Elements:get("Rectangle524"), table("x"=91.0+OffsetX, "y"=77.0+OffsetY, "w"=40.0, "h"=4.0, "r"=255, "g"=255, "b"=255, "radius"=0))
        box(EGP, Elements:get("Rectangle526"), table("x"=90.0+OffsetX, "y"=51.0+OffsetY, "w"=42.0, "h"=6.0, "r"=0, "g"=0, "b"=0, "radius"=0))
        box(EGP, Elements:get("Rectangle527"), table("x"=91.0+OffsetX, "y"=52.0+OffsetY, "w"=40.0, "h"=4.0, "r"=255, "g"=255, "b"=255, "radius"=0))
        box(EGP, Elements:get("Rectangle528"), table("x"=97.0+OffsetX, "y"=47.0+OffsetY, "w"=28.0, "h"=38.0, "r"=0, "g"=0, "b"=0, "radius"=1.0))
        box(EGP, Elements:get("Rectangle529"), table("x"=98.0+OffsetX, "y"=48.0+OffsetY, "w"=26.0, "h"=36.0, "r"=255, "g"=255, "b"=255, "radius"=1.0))
        box(EGP, Elements:get("Rectangle530"), table("x"=100.0+OffsetX, "y"=50.0+OffsetY, "w"=22.0, "h"=8.0, "r"=0, "g"=0, "b"=0, "radius"=1.0))
        box(EGP, Elements:get("Rectangle532"), table("x"=100.0+OffsetX, "y"=60.0+OffsetY, "w"=22.0, "h"=23.0, "r"=0, "g"=0, "b"=0, "radius"=1.0))
        box(EGP, Elements:get("Rectangle533"), table("x"=101.0+OffsetX, "y"=61.0+OffsetY, "w"=20.0, "h"=21.0, "r"=255, "g"=255, "b"=255, "radius"=1.0))
        box(EGP, Elements:get("Rectangle534"), table("x"=102.0+OffsetX, "y"=62.0+OffsetY, "w"=18.0, "h"=19.0, "r"=0, "g"=0, "b"=0, "radius"=0))
        line(EGP, Elements:get("Line12"), table("x"=102.0+OffsetX, "y"=67.0+OffsetY, "w"=18.0, "h"=1.0, "r"=255, "g"=255, "b"=255))
        line(EGP, Elements:get("Line13"), table("x"=102.0+OffsetX, "y"=72.0+OffsetY, "w"=18.0, "h"=1.0, "r"=255, "g"=255, "b"=255))
        line(EGP, Elements:get("Line14"), table("x"=102.0+OffsetX, "y"=77.0+OffsetY, "w"=18.0, "h"=1.0, "r"=255, "g"=255, "b"=255))
        line(EGP, Elements:get("Line15"), table("x"=109.0+OffsetX, "y"=62.0+OffsetY, "w"=19.0, "h"=1.0, "r"=255, "g"=255, "b"=255))
        EGP:egpAngle(Elements:get("Line15"), -90)
        line(EGP, Elements:get("Line16"), table("x"=116.0+OffsetX, "y"=62.0+OffsetY, "w"=19.0, "h"=1.0, "r"=255, "g"=255, "b"=255))
        EGP:egpAngle(Elements:get("Line16"), -90)
        box(EGP, Elements:get("Rectangle535"), table("x"=103.0+OffsetX, "y"=77.0+OffsetY, "w"=5.0, "h"=4.0, "r"=255, "g"=255, "b"=255, "radius"=0))
        box(EGP, Elements:get("Rectangle536"), table("x"=116.0+OffsetX, "y"=77.0+OffsetY, "w"=5.0, "h"=4.0, "r"=255, "g"=255, "b"=255, "radius"=0))
        line(EGP, Elements:get("Line17"), table("x"=116.0+OffsetX, "y"=37.0+OffsetY, "w"=8.0, "h"=1.0, "r"=0, "g"=0, "b"=0))
        EGP:egpAngle(Elements:get("Line17"), -90)
        line(EGP, Elements:get("Line19"), table("x"=113.0+OffsetX, "y"=37.0+OffsetY, "w"=7.0, "h"=1.0, "r"=0, "g"=0, "b"=0))
        EGP:egpAngle(Elements:get("Line19"), -90)
        line(EGP, Elements:get("Line20"), table("x"=110.0+OffsetX, "y"=37.0+OffsetY, "w"=8.0, "h"=1.0, "r"=0, "g"=0, "b"=0))
        EGP:egpAngle(Elements:get("Line20"), -90)
        line(EGP, Elements:get("Line21"), table("x"=107.0+OffsetX, "y"=37.0+OffsetY, "w"=8.0, "h"=1.0, "r"=0, "g"=0, "b"=0))
        EGP:egpAngle(Elements:get("Line21"), -90)
        line(EGP, Elements:get("Line22"), table("x"=104.0+OffsetX, "y"=37.0+OffsetY, "w"=8.0, "h"=1.0, "r"=0, "g"=0, "b"=0))
        EGP:egpAngle(Elements:get("Line22"), -90)
        line(EGP, Elements:get("Line18"), table("x"=119.0+OffsetX, "y"=37.0+OffsetY, "w"=8.0, "h"=1.0, "r"=0, "g"=0, "b"=0))
        EGP:egpAngle(Elements:get("Line18"), -90)
        line(EGP, Elements:get("Line23"), table("x"=101.0+OffsetX, "y"=37.0+OffsetY, "w"=20.0, "h"=1.0, "r"=0, "g"=0, "b"=0))
        line(EGP, Elements:get("Line24"), table("x"=102.0+OffsetX, "y"=38.0+OffsetY, "w"=2.0, "h"=1.0, "r"=0, "g"=0, "b"=0))
        EGP:egpAngle(Elements:get("Line24"), 90)
        line(EGP, Elements:get("Line25"), table("x"=121.0+OffsetX, "y"=38.0+OffsetY, "w"=2.0, "h"=1.0, "r"=0, "g"=0, "b"=0))
        EGP:egpAngle(Elements:get("Line25"), 90)
    }

    function void initScreens() {
        SiteA:egpClear()
        SiteB:egpClear()
        
        SiteA:egpDrawTopLeft(1)
        SiteB:egpDrawTopLeft(1)
        
        # White backgrounds
        SiteA:egpBox(100, vec2(0, 0), vec2(512, 512))
        SiteA:egpColor(100, vec(255, 255, 255))
        SiteB:egpBox(100, vec2(0, 0), vec2(512, 512))
        SiteB:egpColor(100, vec(255, 255, 255))
        
        # Site labels
        SiteA:egpText(101, "SITE A", vec2(256, 20))
        SiteA:egpAlign(101, 1, 1)
        SiteA:egpSize(101, 30)
        SiteA:egpColor(101, vec(0, 0, 0))
        
        SiteB:egpText(101, "SITE B", vec2(256, 20))
        SiteB:egpAlign(101, 1, 1)
        SiteB:egpSize(101, 30)
        SiteB:egpColor(101, vec(0, 0, 0))
        
        SiteA:egpAlpha(100,0)
        SiteB:egpAlpha(100,0)
        
    }
    
    function table getPlayerLookingAtScreen() {
        local AllPlayers = players()
        for (I = 1, AllPlayers:count()) {
            local P = AllPlayers[I, entity]
            if (!P:isPlayer() || !P:isAlive()) { continue }
            
            local AimEnt = P:aimEntity()
            
            if (AimEnt:isValid() && AimEnt == SiteA:entity()) {
                SiteA:egpCursor(P)
                return table("player" = P, "site" = "A", "egp" = SiteA, "cursor" = SiteA:egpCursor(P))
            }
            elseif (AimEnt:isValid() && AimEnt == SiteB:entity()) {
                SiteB:egpCursor(P)
                return table("player" = P, "site" = "B", "egp" = SiteB, "cursor" = SiteB:egpCursor(P))
            }
        }
        return table()
    }

    initScreens()
}

# Main loop - check all players looking at screens
event tick() {
    
    local LookData = getPlayerLookingAtScreen()
    if (LookData:count() > 0) {
        local Player = LookData["player", entity]
        local Site = LookData["site", string]
        local Cursor = LookData["cursor", vector2]
        local SteamID = Player:steamID64()
        local IsPressingE = Player:keyUse()
        
        # Planting logic
        if (BombState == 0 && TeamT:exists(SteamID) && SelectedPlanter:isValid() && Player == SelectedPlanter && IsPressingE) {
            BombState = 1
            PlantProgress = 0
            BombPos = Cursor
            BombSite = Site
            Planter = SteamID
            
            soundPlay(1, 0, "buttons/button14.wav")
            Player:msg("{!orange [BOMB]} Planting...")
            
            timer("plant", 0.1, 0, function() {
                if (BombState != 1) {
                    stoptimer("plant")
                    return
                }
                
                local CurrentPlayer = findPlayerBySteamID64(Planter)
                if (!CurrentPlayer:isValid() || !CurrentPlayer:isAlive()) {
                    BombState = 0
                    PlantProgress = 0
                    stoptimer("plant")
                    return
                }
                
                # Check if still looking at screen and pressing E
                if (!CurrentPlayer:keyUse()) {
                    BombState = 0
                    PlantProgress = 0
                    CurrentPlayer:msg("{!red [BOMB]} Planting cancelled - released E!")
                    stoptimer("plant")
                    return
                }
                
                local CurrentLookData = getPlayerLookingAtScreen()
                if (CurrentLookData:count() == 0 || CurrentLookData["player", entity] != CurrentPlayer || CurrentLookData["site", string] != BombSite) {
                    BombState = 0
                    PlantProgress = 0
                    CurrentPlayer:msg("{!red [BOMB]} Planting cancelled - looked away!")
                    stoptimer("plant")
                    return
                }
                
                local CurrentCursor = CurrentLookData["cursor", vector2]
                if (CurrentCursor:distance(BombPos) > 5) {
                    BombState = 0
                    PlantProgress = 0
                    CurrentPlayer:msg("{!red [BOMB]} Planting cancelled - moved cursor!")
                    stoptimer("plant")
                    return
                }
                
                PlantProgress += 0.1
                
                if (PlantProgress >= 5) {
                    BombState = 2
                    if (BombSite == "A") {
                        drawBomb(SiteA, BombPos:x(), BombPos:y())
                    } else {
                        drawBomb(SiteB, BombPos:x(), BombPos:y())
                    }
                    soundPlay(2, 0, "buttons/button9.wav")
                    CurrentPlayer:msg("{!green [BOMB]} Bomb planted at Site " + BombSite + "!")
                    stoptimer("plant")
                }
            })
        }
        
        # Defusing logic
        if (BombState == 2 && TeamCT:exists(SteamID) && Site == BombSite && IsPressingE) {
            if (Cursor:distance(BombPos) <= 30) {
                BombState = 3
                DefuseProgress = 0
                Defuser = SteamID
                
                soundPlay(3, 0, "buttons/button14.wav")
                Player:msg("{!blue [BOMB]} Defusing...")
                
                timer("defuse", 0.1, 0, function() {
                    if (BombState != 3) {
                        stoptimer("defuse")
                        return
                    }
                    
                    local CurrentPlayer = findPlayerBySteamID64(Defuser)
                    if (!CurrentPlayer:isValid() || !CurrentPlayer:isAlive()) {
                        BombState = 2
                        DefuseProgress = 0
                        stoptimer("defuse")
                        return
                    }
                    
                    # Check if still looking at screen and pressing E
                    if (!CurrentPlayer:keyUse()) {
                        BombState = 2
                        DefuseProgress = 0
                        CurrentPlayer:msg("{!red [BOMB]} Defusing cancelled - released E!")
                        stoptimer("defuse")
                        return
                    }
                    
                    local CurrentLookData = getPlayerLookingAtScreen()
                    if (CurrentLookData:count() == 0 || CurrentLookData["player", entity] != CurrentPlayer || CurrentLookData["site", string] != BombSite) {
                        BombState = 2
                        DefuseProgress = 0
                        CurrentPlayer:msg("{!red [BOMB]} Defusing cancelled - looked away!")
                        stoptimer("defuse")
                        return
                    }
                    
                    local CurrentCursor = CurrentLookData["cursor", vector2]
                    if (CurrentCursor:distance(BombPos) > 30) {
                        BombState = 2
                        DefuseProgress = 0
                        CurrentPlayer:msg("{!red [BOMB]} Defusing cancelled - moved away from bomb!")
                        stoptimer("defuse")
                        return
                    }
                    
                    DefuseProgress += 0.1
                    
                    if (DefuseProgress >= 5) {
                        BombState = 4
                        if (BombSite == "A") {
                            SiteA:clearScreen()
                        } else {
                            SiteB:clearScreen()
                        }
                        soundPlay(4, 0, "buttons/button3.wav")
                        CurrentPlayer:msg("{!green [BOMB]} Bomb defused!")
                        stoptimer("defuse")
                        
                        timer("reset", 3, function() {
                            BombState = 0
                            BombPos = vec2(0, 0)
                            BombSite = ""
                            Planter = ""
                            Defuser = ""
                            SelectedPlanter = _NO_ENTITY
                            initScreens()
                        })
                    }
                })
            }
        }
    }
}

# Chat commands
event chat(Player:entity, Message:string, Team:number) {
    local Msg = Message:upper()
    local SteamID = Player:steamID64()
    
    if (Msg == "!JOINT") {
        if (TeamCT:exists(SteamID)) {
            TeamCT:removeString(SteamID)
        }
        TeamT[SteamID, string] = Player:name()
        Player:msg("{!orange [BOMB]} Joined Terrorist team!")
    }
    
    elseif (Msg == "!JOINCT") {
        if (TeamT:exists(SteamID)) {
            TeamT:removeString(SteamID)
        }
        TeamCT[SteamID, string] = Player:name()
        Player:msg("{!blue [BOMB]} Joined Counter-Terrorist team!")
    }
    
    elseif (Msg == "!START" && Player == owner()) {
        if (TeamT:count() == 0) {
            Player:msg("{!red [BOMB]} No terrorists in game!")
            break
        }
        
        local TList = TeamT:clone()
        local RandomIndex = randint(1, TList:count())
        local Counter = 0
        
        foreach (K:string, V:string = TList) {
            Counter++
            if (Counter == RandomIndex) {
                SelectedPlanter = findPlayerBySteamID64(K)
                if (SelectedPlanter:isValid()) {
                    SelectedPlanter:msg("{!green [BOMB]} You have been selected to plant the bomb!")
                    Player:msg("{!green [BOMB]} " + V + " selected as bomber!")
                } else {
                    Player:msg("{!red [BOMB]} Selected player not found!")
                }
                break
            }
        }
    }
}
